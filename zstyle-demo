#!/usr/bin/env zsh
0="${${ZERO:-${0:#$ZSH_ARGZERO}}:-${(%):-%N}}"
0="${${(M)0:#/*}:-$PWD/$0}"

# load library functions
source ls-colors.zsh ''

# to name the functions with a different namespace
# call source with a different argument
#source my-plugin::ls

# Same as :completion:* list-colors
zstyle '*' list-colors "${(@s[:])LS_COLORS}" '*.md=35'

# emable extendedglob under certain contexts:
zstyle ':demo:some-context' list-colors-extended true

# set extra definitions in certain contexts:
zstyle ':demo:some-context' list-colors "${(@s[:])LS_COLORS}" '(#i)(*/|)license(|.*)=04'
# (underline LICENSE, or license.txt, or similar)

# color each file in the argument list
print 'In :demo:some-context'
for file;(){
	local REPLY
	ls-color::lookup :demo:some-context "$file"
	# point to symlink resolution if it exists
	print '  \e['$REPLY'm'$file'\e[0m '
}
print '\nIn :demo:other-context'
for file;(){
	local REPLY
	ls-color::lookup :demo:other-context "$file"
	# point to symlink resolution if it exists
	print '  \e['$REPLY'm'$file'\e[0m '
}

