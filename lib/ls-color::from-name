#!/usr/bin/env zsh
# Usage:
# $1: filename
# $2: if 0 or empty, lookup using lstat (do not follow symlink)
#     if 1,          lookup using  stat (do     follow symlink)
#     if 2,          lookup using both
# $3: array name. If empty, use $reply
#
# Either sets reply=( $code [ $code ] ) depending on $2
# or returns non-zero

emulate -L zsh

local -a stat
local REPLY
zmodload -F zsh/stat b:zstat
case $2 in
0|'')
	zstat -A stat -L $1 || return 1
	ls-color::from-mode $1 $stat[3]
	reply=($REPLY)
;;
1)
	zstat -A stat    $1 || return 1
	ls-color::from-mode $1 $stat[3]
	reply=($REPLY)
;;
2)
	zstat -A stat -L $1 || return 1
	ls-color::from-mode $1 $stat[3]
	reply=($REPLY)
	zstat -A stat    $1 || return 1
	ls-color::from-mode $1 $stat[3]
	reply+=($REPLY)
;;
*) return 2 ;;
esac
